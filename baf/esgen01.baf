IF
	See(NearestEnemyOf(Myself))
	Global("Prep","LOCALS",0)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		ApplySpell(Myself,WIZARD_IMPROVED_MANTLE)
		ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
                ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobal("Prep","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	Global("SpellTrigger","LOCALS",0)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		DisplayString(Myself,~Disparador de conjuros - activado~)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
		DisplayString(Myself,~Disparador de conjuros - Escudo ígneo (rojo)~)
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
		DisplayString(Myself,~Disparador de conjuros - Globo de invulnerabilidad~)
		ReallyForceSpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
		SetGlobal("SpellTrigger","LOCALS",1)
END

IF
	See([GOODCUTOFF.0.0.0.0.SUMMONED])
	Global("summon","LOCALS",0)
THEN
	RESPONSE #100
		FaceObject(LastSeenBy(Myself))
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)
		SetGlobal("summon","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_METEOR_SWARM)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_METEOR_SWARM)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_GATE)
THEN
	RESPONSE #100
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_EVIL)
		Spell(Myself,WIZARD_GATE)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
	See(NearestEnemyOf(Myself))
	StateCheck(Myself,STATE_SILENCED)
	HaveSpell(WIZARD_VOCALIZE)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_VOCALIZE)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_SUN_FIRE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_SUN_FIRE)
END

IF
	Allegiance(Myself,ENEMY)
	Detect([PC])
	!See(LastSeenBy(Myself))
	HaveSpell(WIZARD_TRUE_SIGHT)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_TRUE_SIGHT)
END

IF
	Allegiance(Myself,ENEMY)
	Detect([PC])
	!See(LastSeenBy(Myself))
	!HaveSpell(WIZARD_TRUE_SIGHT)
	Delay(10)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Prep","LOCALS",1)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SPELL_TURNING)
		SetGlobal("Prep","LOCALS",2)
END

IF
	!HasBounceEffects(Myself)
	!HasImmunityEffects(Myself)
	HaveSpell(WIZARD_SPELL_TURNING)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SPELL_TURNING)
END

IF
	See(NearestEnemyOf(Myself))
	TookDamage()
	Global("Prep2","LOCALS",0)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		DisplayString(Myself,~Contingencia en cadena - Manto mejorado~)
		ApplySpell(Myself,WIZARD_IMPROVED_MANTLE)
		DisplayString(Myself,~Contingencia en cadena - Retorno de conjuros~)
		ApplySpell(Myself,WIZARD_SPELL_TURNING)
		DisplayString(Myself,~Contingencia en cadena - Doble engañoso~)
		ApplySpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
		SetGlobal("Prep2","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	TookDamage()
	Global("Prep2","LOCALS",1)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		DisplayString(Myself,~Contingencia - Protección contra armas mágicas~)
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobal("Prep2","LOCALS",2)
END

IF
	Allegiance(Myself,ENEMY)
	!HasBounceEffects(Myself)
	!HasImmunityEffects(Myself)
	HaveSpell(WIZARD_SPELL_TURNING)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SPELL_TURNING)
END

IF
	See(NearestEnemyOf(Myself))
	Global("SpellTrigger","LOCALS",0)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		DisplayString(Myself,~Secuenciador de conjuros - activado~)
		DisplayString(Myself,~Secuenciador de conjuros - Confusión~)
		ReallyForceSpell(SecondNearestEnemyOf(Myself),WIZARD_CONFUSION)
		DisplayString(Myself,~Secuenciador de conjuros -  Bola de fuego~)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
		DisplayString(Myself,~Secuenciador de conjuros -  Bola de fuego~)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
                DisplayString(Myself,~Secuenciador de conjuros -  Bola de fuego~)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
		SetGlobal("SpellTrigger","LOCALS",1)
END


IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_TIME_STOP)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_TIME_STOP)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_NPC_SYMBOL_STUN)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_NPC_SYMBOL_STUN)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_NPC_SYMBOL_FEAR)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_NPC_SYMBOL_FEAR)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_NPC_SYMBOL_DEATH)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_NPC_SYMBOL_DEATH)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_SUN_FIRE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_SUN_FIRE)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_FLESH_TO_STONE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_FLESH_TO_STONE)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_DISINTEGRATE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_DISINTEGRATE)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_FIRE_SHIELD_RED)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_FIRE_SHIELD_RED)
END

IF
	See([PC])
	Allegiance(Myself,ENEMY)
	Delay(18)
THEN
	RESPONSE #100
		ReallyForceSpell(SixthNearest([PC]),WIZARD_FIREBALL)
END

IF
	See([PC])
	Allegiance(Myself,ENEMY)
	Delay(4)
THEN
	RESPONSE #100
		ReallyForceSpell(SecondNearest([PC]),WIZARD_AGANNAZAR_SCORCHER)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_FLAME_ARROW)
	Global("arrow","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_FLAME_ARROW)
		SetGlobal("arrow","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	Delay(11)
	GlobalLT("EfreetiInvisibility","LOCALS",3)
THEN
	RESPONSE #100
		IncrementGlobal("EfreetiInvisibility","LOCALS",1)
		ForceSpell(Myself,WIZARD_INVISIBILITY)
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),5)
END

